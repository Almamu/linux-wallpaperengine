# Maintainer: Alexis Maiquez <aur@almamu.com>
pkgname=linux-wallpaperengine-git
_pkgname=linux-wallpaperengine
pkgver=r567.b6c1825
pkgrel=1
pkgdesc="use steam's wallpaperengine on linux"
arch=('x86_64')
url="https://github.com/Almamu/linux-wallpaperengine"
license=('GPL-3.0-only')
depends=('lz4' 'ffmpeg' 'mpv' 'glfw' 'glew' 'freeglut' 'libpulse' 'libcups' 'at-spi2-core' 'nss' 'libxcomposite' 'libxdamage' 'nspr')
makedepends=('git' 'cmake' 'sdl2' 'glm')
provides=("linux-wallpaperengine")
source=("${pkgname}::git+https://github.com/Almamu/linux-wallpaperengine.git#branch=main")
sha512sums=('SKIP')
optdepends=(
    'xorg-xrandr: support for X11'
    'wayland-protocols: support for wayland')

pkgver() {
    cd "$pkgname"
        ( set -o pipefail
          git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
          printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
        )
}

prepare() {
    cd "$pkgname"
    git submodule update --init --recursive
    git -c protocol.file.allow=always submodule update
}

build() {
    cmake -B build -S "$pkgname" \
        -DCMAKE_BUILD_TYPE='Release' \
        -DCMAKE_INSTALL_PREFIX="/opt/${_pkgname}" \
        -Wno-dev \
        -DCMAKE_CXX_FLAGS="-ffat-lto-objects -Wno-builtin-macro-redefined" \
        -DCMAKE_C_FLAGS="-Wno-builtin-macro-redefined"
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    # create forwarding script
    install -d -m755 "${pkgdir}/usr/bin"
    cat > "${pkgdir}/usr/bin/${_pkgname}" << EOF
#!/bin/bash
export LD_LIBRARY_PATH="/opt/${_pkgname}/lib:\$LD_LIBRARY_PATH"
cd /opt/${_pkgname}; ./${_pkgname} "\$@"
EOF
    chmod +x ${pkgdir}/usr/bin/${_pkgname}
    chmod +x ${pkgdir}/opt/${_pkgname}/linux-wallpaperengine
}
